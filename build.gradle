subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    ext {
        jacksonVersion = '2.4.3'
        rhinoVersion = '1.7.7'
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        // Make sure warnings are on
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    version = "$majorVersion.$buildNumber"
    group = "com.loadtestgo";

    jar {
        manifest {
            attributes 'Implementation-Title': 'PizzaScript', 'Implementation-Version': version
        }
    }

    repositories {
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            // Fail if we use multiple version of the same library in the same
            // project.
            failOnVersionConflict()
        }
    }

    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile 'org.jmockit:jmockit:1.20'
    }
}

project(':script-api') {
    dependencies {
        compile project(':util')
        compile "org.mozilla:rhino:$rhinoVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    }
}

project(':util') {
    dependencies {
        compile 'commons-io:commons-io:2.4'
        compile 'org.tinylog:tinylog:1.0'
    }
}

project(':mov-writer') {
}

project(':websocket') {
}

project(':script-engine') {
    dependencies {
        compile project(':script-api')
        compile project(':util')
        compile project(':websocket')
        compile project(':mov-writer')
        compile "org.mozilla:rhino:$rhinoVersion"
        compile 'commons-io:commons-io:2.4'
        compile 'javax.servlet:javax.servlet-api:3.1.0'
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        compile 'org.json:json:20140107'
        compile 'com.google.collections:google-collections:1.0'
    }

    if (project.hasProperty("release")) {
        processResources {
            exclude('chrome/extension/pizza/*.js')
            exclude('chrome/extension/pizza/manifest.json')
        }
        task minify(type: Exec) {
            commandLine '/usr/local/bin/gulp', 'enginejs'
        }
        processResources.dependsOn minify
    }
}

project(':script-editor') {
    apply plugin: 'application'
    mainClassName = "com.loadtestgo.script.editor.PizzaScript"
    applicationName = "pizzascript-ide"

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            // RsyntaxTextArea/languagesupport brings in 1.7.6
            force "org.mozilla:rhino:$rhinoVersion"
        }
    }

    // Compress tar file
    dependencies {
        compile project(':script-api')
        compile project(':script-engine')
        compile project(':util')
        compile project(':view-metrics')
        compile 'jline:jline:2.10'
        compile 'com.fifesoft:rsyntaxtextarea:2.5.8'
        compile 'com.fifesoft:autocomplete:2.5.8'
        compile 'com.fifesoft:languagesupport:2.5.8'
    }
    distTar {
        compression = Compression.GZIP
    }
}

project(':script-runner') {
    apply plugin: 'application'
    mainClassName = "com.loadtestgo.script.runner.Main"
    applicationName = "pizzascript"

    // Compress tar file
    dependencies {
        compile project(':script-api')
        compile project(':script-engine')
        compile project(':util')
        compile project(':view-metrics')
        compile 'jline:jline:2.10'
        compile 'org.fusesource.jansi:jansi-project:1.13'
    }
    distTar {
        compression = Compression.GZIP
    }
}

project(':view-metrics') {
    if (project.hasProperty("release")) {
        processResources {
            exclude('js/*.js')
        }
        task minify(type: Exec) {
            commandLine 'gulp', 'view-metrics'
        }
        processResources.dependsOn minify
    }
}

project(':script-tester') {
    dependencies {
        compile project(':script-api')
        compile project(':script-engine')
        compile project(':util')
        compile project(':view-metrics')
        compile 'junit:junit:4.+'
    }
    configurations {
      integrationCompile.extendsFrom testCompile
      integrationRuntime.extendsFrom testRuntime
    }
    sourceSets {
        integration {
            java.srcDir file('src/main/java')
            resources.srcDir file('src/main/resources')
        }
    }
    task integration(type: Test) {
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        testClassesDir = sourceSets.integration.output.classesDir
        classpath = sourceSets.integration.runtimeClasspath
    }
}

project(':dist') {
  apply plugin: 'java-library-distribution'

  def projects = [ project(':script-runner'), project(':script-editor') ]
  distributions {
      main {
          baseName = "pizzascript"
          contents {
              exclude "dist-${version}.jar"
              exclude ".gitkeep"

              into('bin') {
                  def files = []
                  projects.each { p->
                      Task t = p.tasks.getByName("startScripts")
                      files.addAll(t.outputs)
                  }
                  from files
                  fileMode = 0755
              }
              into('lib') {
                  def libs = []

                  projects.each { p->
                    libs.addAll(p.configurations.compile.resolvedConfiguration.resolvedArtifacts.file)
                    libs << p.jar
                  }

                  from libs.unique()
              }
              into('scripts') {
                  from(fileTree(dir: '../scripts', includes: ['*.js']))
              }
              into('docs') {
                  from(fileTree(dir: '../docs/api'))
                  from(file('../script-api/pizzascript.js'))
              }
          }
      }
  }
  distTar {
      compression = Compression.GZIP
  }
}

task baseDocs(type:Exec) {
  commandLine 'node_modules/.bin/jsdoc', '-c', 'script-api/jsdoc.conf', '-t', 'node_modules/jsdoc-baseline', 'script-api/pizzascript.js', 'script-api/pizzascript.md', '-d', 'docs/api'
}

task docs(dependsOn:baseDocs) << {
  FileTree tree = fileTree (dir: "docs/api");
  tree.include "*.html"
  tree.each { File file ->
    String contents = file.getText( 'UTF-8' )
    contents = contents.replaceAll('<title>Home</title>', '<title>PizzaScript Browser Automation API</title>' )
    contents = contents.replaceAll('<title>Module:', '<title>PizzaScript |' )
    contents = contents.replaceAll('href="index.html"', 'href="http://pizzascript.org"')
    contents = contents.replaceAll('Home</a>', 'PizzaScript</a><a class="jsdoc-navbar-package-name" href="index.html">Index</a>')
    contents = contents.replaceAll('<span class="label label-kind">module</span>', '')

    file.write( contents, 'UTF-8' )
  }
}

task 'incVersion' << {
    Properties props = new Properties()
    File propsFile = new File('gradle.properties')
    props.load(propsFile.newDataInputStream())
    Integer nextbuildnum = (((props.getProperty('buildNumber')) as BigDecimal) + 1)
    props.setProperty('buildNumber', nextbuildnum.toString())
    props.store(propsFile.newWriter(), null)
    props.load(propsFile.newDataInputStream())
}
